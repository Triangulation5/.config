pragma Singleton

import QtQuick
import Quickshell
import "."

/**
 * Lockscreen palette.
 *
 * Matches the pill theme automatically by binding every exported color to the
 * shared Flags/Dyn singletons. Changing the palette mode or wallpaper updates
 * all consumers without restarting.
 */
Singleton {
    readonly property bool dyn: Flags.paletteMode !== "static"

    readonly property color verm:
        dyn ? Qt.darker(Dyn.primary, 1.18) : "#c0442b"

    readonly property color cream:
        dyn ? Dyn.cream : "#e6d6cb"

    readonly property color bright:
        dyn ? Dyn.bright : "#fff6f0"

    readonly property color dim:
        dyn ? Dyn.dim : "#8a7d74"

    readonly property string font: "Inter"

    readonly property color fieldBg:
        dyn
            ? Qt.alpha(bright, 0.10)
            : Qt.rgba(1.0, 0.96, 0.94, 0.10)

    readonly property color fieldBorder:
        dyn
            ? Qt.alpha(cream, 0.30)
            : Qt.rgba(230 / 255, 214 / 255, 203 / 255, 0.30)

    readonly property color trackBg:
        dyn
            ? Qt.alpha(cream, 0.16)
            : Qt.rgba(240 / 255, 224 / 255, 215 / 255, 0.16)

    readonly property color error:
        dyn ? Dyn.primary : "#e0563b"

    readonly property color placeholder:
        dyn ? Dyn.faint : "#606079"

    readonly property color capsule:
        dyn
            ? Dyn.surface
            : Qt.rgba(20 / 255, 20 / 255, 21 / 255, 0.85)

    readonly property color capsuleBorder:
        dyn
            ? Qt.rgba(
                  Dyn.outlineVariant.r,
                  Dyn.outlineVariant.g,
                  Dyn.outlineVariant.b,
                  0.50
              )
            : Qt.rgba(96 / 255, 96 / 255, 121 / 255, 0.55)
}
